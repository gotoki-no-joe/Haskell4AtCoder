# 7.Union-Find \(7\)

具体的に数え上げられる対象を互いに素に分割している状況で、分割の統合\(union\)と、現在の状況で２つの要素が同じ分割にいるか判定 \(find\)が交互に繰り返されるときに、状況を追跡し判定を高速に行える技法。

TL;DR 結局、mutable unboxed vectorを用いて命令的で実現しないと性能が出なかった。実装は末尾。

要素を配列の添字として、配列の要素は、分割の代表元に至るリンクとする。つまり、葉から根への辺とみると、ひとつの分割が木、全体が森をなす。ただしこれは配列に埋め込まれていることが本質であって、Haskell的な木構造とは結びつかない。リンクの初期値は自分自身とする。

ふたつの要素を指定してのfindは、リンクを辿ることでそれぞれの代表元を求め、等しければ同じ分割にある。

ふたつの要素を指定してのunionは、やはりまずそれぞれの代表元を求め、いずれかの親をもう一方に更新する。

木が一本道になると効率が出ない。その対処は二通り知られており、両方を組み合わせることもできるが、片方だけでもそれなりに動作するらしい。

ひとつは経路圧縮という。要素に対して代表元を探すときに通った経路の要素全てについて、それらを代表元の直接の子になるようにリンクをつなぎ直す。これはmutableな配列であることを前提としているのでHaskellとは相性がよくない。

もうひとつは木の高さを意識して、書き換えは低い方に対して行うようにするものである。これなら、unionのときだけ更新が起きるのでHaskellで採用しやすい。

単純化のため、要素は0始まりの自然数で指定するとする。

効率化のない単純な版は容易に書ける。  
木が一本道になったとき、`getRoot`に$$O(n)$$かかる。

```haskell
import qualified Data.Vector as V

type UnionFind = V.Vector Int

newUF :: Int -> UnionFind
newUF ub = V.generate ub id

getRoot :: UnionFind -> Int -> Int
getRoot vec i
  | i == k = i
  | True   = getRoot vec k
  where
    k = vec V.! i

findUF :: UnionFind -> Int -> Int -> Bool
findUF vec a b = getRoot vec a == getRoot vec b

uniteUF :: UnionFind -> Int -> Int -> UnionFind
uniteUF vec i k
  | a == b = vec
  | True   = vec V.// [(a,b)]
  where
    a = getRoot vec i
    b = getRoot vec k
```

木の高さ（ランク）を管理して効率をめざす版はこうなる。  
この場合木の高さが$$O(\log n)$$で抑えられるらしい。

```haskell
import qualified Data.Vector as V

-- @gotoki_no_joe
type UnionFind = V.Vector (Int,Int) -- Parent, Rank

-- 0～ub-1の要素が孤立している初期状態
newUF :: Int -> UnionFind
newUF ub = V.generate ub (\i -> (i,0))

getRoot :: UnionFind -> Int -> Int
getRoot vec i
  | i == k = i
  | True   = getRoot vec k
  where
    k = fst $ vec V.! i

-- vecでaとbが同じ分割にいるか判定
findUF :: UnionFind -> Int -> Int -> Bool
findUF vec a b = getRoot vec a == getRoot vec b

-- vecでiの分割とkの分割を統合
uniteUF :: UnionFind -> Int -> Int -> UnionFind
uniteUF vec i k
  | a == b = vec
  | True   = case compare r s of
               LT -> vec V.// [(a,(b,r))]
               GT -> vec V.// [(b,(a,s))]
               EQ -> vec V.// [(b,(a,s)), (a,(a,succ r))] -- どっちに繋いでもいいが
  where
    a = getRoot vec i
    r = snd $ vec V.! a
    b = getRoot vec k
    s = snd $ vec V.! b
```

### 関連問題

ABC177D [ACコード](https://atcoder.jp/contests/abc177/submissions/22742331) Unboxed.Mutable, 根にはサイズを付与  
ACL Beginner Contest C  
yukicoder No.1390 Get together  
ARC106 B  
MojaCoder Bonsai  
ARC114 B  
ABC157 D  
ABC120 D  
ARC111 B  
ABC183 F 「dictの知識が必要」?

### 延長戦

ABC177Dでは、各グループのサイズが最終的に必要となる。ここで、そもそも木のバランスをとるのに、木のランクではなく木のサイズを使う近似解法も通用するという。さて、木のサイズやランクは、木の葉では把握できない。根だけがそれを持てて、持つ必要がある。また、根は親を持たないので、親へのリンクとランクまたはサイズは排他的な情報にできる。真面目にHaskellでするなら

```haskell
type UnionFind = V.Vector (Either Int Int) -- Left Rank / Right Parent
```

となるが、不真面目なプログラマはここで数の符号を悪用するのだそうだ。  
しかしその程度ではまだ、immutable vector の \(//\) がO\(n+m\) で遅すぎるのか、経路圧縮をしないのがいけないのか、通してくれないので、経路圧縮を試みる。そのためには、mutable vector を使う必要が生じて、Pure Haskellでなくなる。

```haskell
import qualified Data.Vector.Unboxed.Mutable as VUM
import Control.Monad

-- @gotoki_no_joe
type UnionFind = VUM.IOVector Int -- Positive : Parent, Negative : Size

-- 0～ub-1の要素が孤立している初期状態
newUF :: Int -> IO UnionFind
newUF ub = VUM.replicate ub (-1)

-- 経路圧縮もする
getRoot :: UnionFind -> Int -> IO Int
getRoot vec i =
  do
    k <- VUM.read vec i
    if k < 0 then return i else
      do
        m <- getRoot vec k
        VUM.write vec i m
        return m

-- vecでaとbが同じ分割にいるか判定
findUF :: UnionFind -> Int -> Int -> IO Bool
findUF vec a b =
  do
    m <- getRoot vec a
    n <- getRoot vec b
    return (m == n)

-- vecでiの分割とkの分割を統合
uniteUF :: UnionFind -> Int -> Int -> IO ()
uniteUF vec i k =
  do
    a <- getRoot vec i
    b <- getRoot vec k
    if a == b then return () else
      do
        r <- VUM.read vec a
        s <- VUM.read vec b
        if r < s then
          do
            VUM.write vec a b
            VUM.write vec b (r+s)
          else do
            VUM.write vec b a
            VUM.write vec a (r+s)
```

