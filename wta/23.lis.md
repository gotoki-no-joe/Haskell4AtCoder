---
description: 'Longest Increasing Subsequence, LIS'
---

# 23.最長増加部分列 \(2\)

{% embed url="https://tjkendev.github.io/procon-library/python/dp/lis.html" %}

長さ$$N$$ の数列$$a_1, a_2, \dots, a_N $$に対して、  
$$a_{i_1} < a_{i_2} < \dots < a_{i_k}$$となる$$i_1 < i_2 < \dots < i_k$$の中で最大の$$k$$を求める

つまり、飛び飛びにより大きい要素を拾って作れる最も長い部分列を作れということ。

数列を前からスキャンして、そこまでの数列に関する増加部分列の末尾要素の候補を一つ持ってくるたびに、それを連結することで数列の長さと最後尾の要素がいくつになるかを追跡する。

Haskellの都合で、後ろから考える。つまりロジックを逆にする。  
$$a_{i+1}$$から$$a_N$$までの数列の増加部分列に、長さ$$k$$先頭の値$$x$$のものがあるとする。次に調べる値$$a_i$$に関して、$$a_i \geq x$$ならばこの列の前に$$a_i$$を繋げたものは増加列ではないのでお呼びでない。一方$$a_i < x$$ならばこの列の前に$$a_i$$を繋げると、長さ$$k+1$$で先頭の値$$a_i$$の増加部分列が見つかる。また、$$x > a_{i-1} > a_i$$でこちらを選ぶ方が得策かもしれないので、両方の情報を残す。また、長さ0の部分列に$$a_i$$を繋げて長さ$$1$$先頭の値$$a_i$$という増加部分列は常に作れる。これで、列に対して全ての増加部分列を見つけ出すことができる。

```haskell
import Data.List
import Data.Function

increasing_subsequences :: Ord a => [a] -> [[a]]
increasing_subsequences = foldr step [ [] ]
  where
    step a iss = [a] : iss ++
                 [a:is | is <- iss, less a is]

less _ [] = True
less x (y:_) = x < y

longuest_increasing_subsequence :: Ord a => [a] -> [a]
longuest_increasing_subsequence =
  maximumBy (compare `on` length) . increasing_subsequence
```

「最長のもの」だけを見つけることが目的なら、全てを持っておく必要はない。それぞれの長さごとに、その長さの部分列の中で、最大の先頭要素を持つものが、以降の成長が最も期待できる。そこで、長さごとに、最大の先頭要素だけを追跡する。

末尾から要素$$a_{i+1}$$まで調べて長さ$$k$$の最大の先頭要素が$$x_k$$、長さ$$k+1$$の最大の先頭要素が$$x_{k+1}$$であるとき、$$a_i < x_k$$ならば長さ$$k+1$$の部分列が作れるが、$$a_i \leq x_{k+1}$$ならば既知の長さ$$k+1$$の列の方がましである。一方$$a_i > x_{k+1}$$のときは長さ$$k+1$$に関する情報を更新するべきである。長さ$$k+1$$の列がまだないときは追加して構わない。

```haskell
import qualified Data.IntMap as IM

longuest_increasing_subsequence :: Ord a => [a] -> (Int,[a]) -- (長さ,lis)
longuest_increasing_subsequence as = IM.findMax m
  where
    m = foldr step (IM.singleton 0 []) as
    step a m = IM.unionWith max m $
               IM.mapKeys succ $ IM.map (a :) $ IM.filter (less a) m

less _ [] = True
less x (y:_) = x < y
```

初期値にmaxBoundを入れたmutableな配列を利用する実装よりは遅いだろう。

列そのものは不要なら、先頭の値だけを管理する。

```haskell
import qualified Data.IntMap as IM

longuest_increasing_subsequence :: Ord a => [a] -> Int -- 長さ
longuest_increasing_subsequence [] = 0
longuest_increasing_subsequence as = fst $ IM.findMax m
  where
    m = foldr step IM.empty as
--  step a m = IM.unionsWith max
--             [ m
--             , IM.singleton 1 a
--             , IM.mapKeys succ $ IM.map (const a) $ IM.filter (a <) m ]
   step a m = IM.unionWith max m $
               IM.fromAscList $ (1,a) : [(succ k, a) | (k,x) <- IM.assocs m, a < x]
```

これではまだ考察がふたつ足らなかった。

まず、mの要素は新たなaにより既存のものより1長くなるか、あるいは既にある長さのものが更新されるだけなので、不連続になることはない。つまり写像でなく配列で管理しても無駄は起きないこと。

もうひとつ、mは真に単調減少すること。  
- 空列は単調減少である。  
- 長さ1の列は単調減少である。  
- 単調増加列に対して、「aを超える要素の次の要素全てを、aと現在の値の大きい方で置き換える」という操作をする。$$x_k > x_{k+1} > a > x_{k+2} > x_{k+3}$$となっていたとき、  
$$x'_{k+1} = \max a \, x_{k+1} = x_{k+1}$$は変化しない。$$x'_{k+2} = \max a \, x_{k+2} = a$$は更新される。これより左の部分も右の部分も変化せず、単調減少のままである。

そして上の考察から、m全体を調べる必要はなく、aをまたぐ一箇所だけを調べて必要なら更新すればよいだけであることが導ける。これで、stepは$$O(n)$$かかる仕事ではなくなる。実際、二分探索で答えを導けるので$$O(\log n)$$となる。

部分列そのものをきれいに求めるためにfoldrを上では用いたのをあきらめ、前からリストをスキャンして最長増加部分列の長さだけを求めるようにした最終版を示す。

```haskell
import qualified Data.IntMap as IM

longuest_increasing_subsequence :: Ord a => [a] -> Int -- 長さ
longuest_increasing_subsequence [] = 0
longuest_increasing_subsequence (a:as) = fst $ IM.findMax m
  where
    m = foldl' step (IM.singleton 1 a) as
    step m a = IM.insertWith min u a m
      where
        u = succ $ binsearch (\i -> a < m IM.! i) 0 $
            succ $ fst $ IM.findMax m
```

binsearchは[二分探索](22.-er-fen-tan-suo-3.md)の関数。  
insertWith min だと、更新しないでよい場合にも同じ値への更新を行いそうな懸念がある。  
stepのwhere節は二分探索でmの中で xl &lt; a &lt; xu となる位置uを探している。

### 関連問題

ABC006D [ACコード](https://atcoder.jp/contests/abc006/submissions/22987707)  
ABC134 E Sequence Decomposing - 【ACコード】  
\(expert!\) AGC024 B Backfront - 【ACコード】

